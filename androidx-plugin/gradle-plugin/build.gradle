import org.jetbrains.kotlin.gradle.tasks.KotlinCompile

plugins {
    id "kotlin"
    id "java-gradle-plugin"
}

ext.supportRootFolder = project.projectDir.getParentFile().getParentFile()

apply from: "../../buildSrc/kotlin-dsl-dependency.gradle"

dependencies {
    implementation(project(":jetpad-integration"))

    compileOnly(findGradleKotlinDsl())
    implementation(gradleApi())

    implementation("org.jetbrains.kotlin:kotlin-stdlib-jdk8:${libs.versions.kotlin.get()}")

    implementation(libs.androidGradlePluginz)
    implementation(libs.dexMemberList)
    implementation(libs.kotlinGradlePluginz)
    implementation(libs.kotlinPoet)

    implementation(libs.dokkaGradlePluginz)
    implementation(libs.gson)

    implementation(libs.androidLint)

    // Not really sure about these:
    implementation("com.googlecode.json-simple:json-simple:1.1")
    implementation("androidx.benchmark:benchmark-gradle-plugin:1.0.0")
    implementation(libs.protobufGradlePluginz)
    implementation(libs.shadow)
    implementation(libs.wireGradlePluginz)
    testImplementation(libs.junit)
}

sourceSets {
    ["public", "private", "plugins"].each { subdir ->
        main.java.srcDirs += "${supportRootFolder}/buildSrc/${subdir}/src/main/kotlin"
    }

    main.java.srcDirs += "${supportRootFolder}/inspection/inspection-gradle-plugin/src/main/kotlin"
    test.java.srcDirs += "${supportRootFolder}/buildSrc-tests/src/test/kotlin"
    main.resources.srcDirs += "${supportRootFolder}/inspection/inspection-gradle-plugin/src/main/resources"
}

version = "0.1.0"
group = "androidx.build"

java {
    sourceCompatibility = JavaVersion.VERSION_11
    targetCompatibility = JavaVersion.VERSION_11
}

tasks.withType(KotlinCompile).all {
    kotlinOptions {
        jvmTarget = "1.8"
        freeCompilerArgs += ["-Werror"]
    }
}

gradlePlugin {
    plugins {
        AndroidXPlugin {
            id = "AndroidXPlugin"
            implementationClass = "androidx.build.AndroidXPlugin"
        }
        AndroidXComposePlugin {
            id = "AndroidXComposePlugin"
            implementationClass = "androidx.build.AndroidXComposePlugin"
        }
    }
}
