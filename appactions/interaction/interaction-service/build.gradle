/*
 * Copyright (C) 2023 The Android Open Source Project
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *      http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

import androidx.build.LibraryType

plugins {
    id("AndroidXPlugin")
    id("com.android.library")
    id("com.google.protobuf")
    id("org.jetbrains.kotlin.android")
}

dependencies {
    implementation(libs.grpcAndroid)
    implementation(libs.grpcBinder)
    implementation(libs.grpcStub)
    implementation(libs.protobufLite)
    implementation("androidx.annotation:annotation:1.1.0")
    implementation("androidx.concurrent:concurrent-futures:1.1.0")
    implementation("androidx.wear.tiles:tiles:1.1.0")
    implementation("javax.annotation:javax.annotation-api:1.3.2")
    // TODO(b/268709908): Bump this to version 1.52.0 and make available from libs.grpcProtobufLite
    implementation("io.grpc:grpc-protobuf-lite:1.45.1")
    implementation(project(":appactions:interaction:interaction-capabilities-core"))
    implementation(project(":appactions:interaction:interaction-proto"))
    implementation(libs.kotlinStdlib)

    testImplementation(libs.kotlinStdlib)
    testImplementation(libs.junit)
    testImplementation(libs.robolectric)
    testImplementation(libs.testExtJunit)
    testImplementation(libs.testExtTruth)
    testImplementation(libs.testCore)
    testImplementation(libs.testRunner)
    testImplementation(libs.truth)
}

protobuf {
    protoc {
        artifact = libs.protobufCompiler.get()
    }
    // Configure the codegen plugins
    plugins {
        grpc {
            artifact = 'io.grpc:protoc-gen-grpc-java:1.52.0'
        }
    }

    // Generates the java proto-lite code for the protos in this project. See
    // https://github.com/google/protobuf-gradle-plugin#customizing-protobuf-compilation
    // for more information.
    generateProtoTasks {
        all().each { task ->
            task.builtins {
                java {
                    option "lite"
                }
            }
            task.plugins {
                grpc {
                    option 'lite'
                }
            }
        }
    }
}

android {
    namespace "androidx.appactions.interaction.service"
    defaultConfig {
        // TODO(b/266649259): Temporary bump to 33 to support capabilities-core. Lower back to 25 to
        // support wear:tiles
        minSdkVersion 33
    }
    defaultConfig {
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }
    testOptions {
        unitTests {
            includeAndroidResources = true
        }
    }
}

androidx {
    name = "androidx.appactions.interaction:interaction-service"
    type = LibraryType.PUBLISHED_LIBRARY
    inceptionYear = "2023"
    description = "Library for integrating with Google Assistant via GRPC binder channel."
}
