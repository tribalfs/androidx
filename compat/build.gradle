import static android.support.dependencies.DependenciesKt.*
import android.support.LibraryGroups
import android.support.LibraryVersions

plugins {
    id("SupportAndroidLibraryPlugin")
}

dependencies {
    api(project(":support-annotations"))
    api (libs.arch_lifecycle_runtime)  {
        exclude module: 'support-annotations'
        transitive = true
    }

    androidTestImplementation(TEST_RUNNER)
    androidTestImplementation(ESPRESSO_CORE)
    androidTestImplementation(MOCKITO_CORE, libs.exclude_bytebuddy) // DexMaker has it"s own MockMaker
    androidTestImplementation(DEXMAKER_MOCKITO, libs.exclude_bytebuddy) // DexMaker has it"s own MockMaker
    androidTestImplementation project(':support-testutils'), {
        exclude group: 'com.android.support', module: 'support-compat'
    }
}

android {
    defaultConfig {
        minSdkVersion 14
    }

    sourceSets {
        main.aidl.srcDirs = ['src/main/java']
        main.res.srcDirs 'res', 'res-public'
    }

    aaptOptions {
        noCompress 'ttf'
    }
}

supportLibrary {
    name = "Android Support Library compat"
    publish = true
    mavenVersion = LibraryVersions.SUPPORT_LIBRARY
    mavenGroup = LibraryGroups.SUPPORT
    inceptionYear = "2015"
    description = "The Support Library is a static library that you can add to your Android application in order to use APIs that are either not available for older platform versions or utility APIs that aren\'t a part of the framework APIs. Compatible on devices running API 14 or later."
    legacySourceLocation = true
}
