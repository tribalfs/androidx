/*
 * Copyright 2019 The Android Open Source Project
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *      http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

import static androidx.build.dependencies.DependenciesKt.*
import androidx.build.CompilationTarget
import androidx.build.LibraryGroups
import androidx.build.LibraryVersions
import androidx.build.Publish

buildscript {
    dependencies {
        classpath 'org.anarres.jarjar:jarjar-gradle:1.0.1'
    }
}

plugins {
    id("AndroidXPlugin")
}

apply plugin: 'org.anarres.jarjar'

configurations {
    jarFiles
}

dependencies {
    jarFiles(project(":compose:compose-compiler-hosted")) {
        transitive = false
    }
}

jarjar.repackage('embeddedPlugin') {
    destinationName "compose-compiler.jar"
    from configurations.jarFiles
    classRename 'com.intellij.**', 'org.jetbrains.kotlin.com.intellij.@1'
}

configurations {
    embeddablePlugin
}

artifacts {
    embeddablePlugin(embeddedPlugin.destinationPath) {
        name "compose-compiler"
        type "jar"
        builtBy embeddedPlugin
    }
}

androidx {
    name = "AndroidX Compose Embedded Compiler Plugin"
    publish = Publish.SNAPSHOT_AND_RELEASE
    toolingProject = true
    mavenVersion = LibraryVersions.COMPOSE
    mavenGroup = LibraryGroups.COMPOSE
    inceptionYear = "2019"
    description = "Contains the Kotlin compiler plugin for Compose"
    compilationTarget = CompilationTarget.HOST
}
