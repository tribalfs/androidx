/*
 * Copyright 2020 The Android Open Source Project
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *      http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

import androidx.build.KmpPlatformsKt
import androidx.build.LibraryType
import androidx.build.RunApiTasks
import androidx.compose.material.icons.generator.tasks.IconGenerationTask

plugins {
    id("AndroidXPlugin")
    id("com.android.library")
    id("AndroidXComposePlugin")
}

def desktopEnabled = KmpPlatformsKt.enableDesktop(project)

IconGenerationTask.registerExtendedIconMainProject(
        project,
        android
)

apply from: "shared-dependencies.gradle"

androidXMultiplatform {
    android()
    if (desktopEnabled) desktop()

    sourceSets {
        commonMain {
            dependencies {
                api(project(":compose:material:material-icons-core"))
                implementation(libs.kotlinStdlibCommon)
                implementation(project(":compose:runtime:runtime"))
            }
        }

        commonTest {
            dependencies {
            }
        }

        jvmMain {
            dependsOn(commonMain)
            dependencies {
            }
        }

        if (desktopEnabled) {
            skikoMain {
                dependsOn(commonMain)
                dependencies {
                }
            }
        }

        androidMain {
            dependsOn(jvmMain)
            dependencies {
            }
        }

        if (desktopEnabled) {
            desktopMain {
                dependsOn(skikoMain)
                dependsOn(jvmMain)
                dependencies {
                }
            }
        }

        jvmTest {
            dependsOn(commonTest)
            dependencies {
            }
        }

        androidAndroidTest {
            dependsOn(jvmTest)
            dependencies {
                implementation(project(":compose:foundation:foundation"))
                implementation(project(":compose:foundation:foundation-layout"))
                implementation(project(":compose:ui:ui"))
                implementation(project(":test:screenshot:screenshot"))
                implementation(project(":compose:ui:ui-test-junit4"))
                implementation(project(":compose:test-utils"))
                implementation("androidx.activity:activity-compose:1.3.1")
                implementation("androidx.appcompat:appcompat:1.3.0")

                implementation(libs.testRules)
                implementation(libs.testRunner)
                implementation(libs.junit)
                implementation(libs.kotlinReflect)
                implementation(libs.truth)
            }
        }

        androidTest {
            dependsOn(jvmTest)
            dependencies {
            }
        }

        if (desktopEnabled) {
            desktopTest {
                dependsOn(jvmTest)
                dependsOn(desktopMain)
                dependencies {
                }
            }
        }
    }
}

configurations {
    embedThemesDebug {
        attributes {
            attribute(iconExportAttr, "true")
            attribute(iconBuildTypeAttr, "debug")
        }
    }
    embedThemesRelease {
        attributes {
            attribute(iconExportAttr, "true")
            attribute(iconBuildTypeAttr, "release")
        }
    }

}

IconGenerationTask.registerExtendedIconThemeProject(project, android)

androidx {
    name = "Compose Material Icons Extended"
    type = LibraryType.PUBLISHED_LIBRARY
    // This module has a large number (5000+) of generated source files and so doc generation /
    // API tracking will simply take too long
    runApiTasks = new RunApiTasks.No("Five thousand generated source files")
    inceptionYear = "2020"
    description = "Compose Material Design extended icons. This module contains all Material icons. It is a very large dependency and should not be included directly."
}

android {
    namespace "androidx.compose.material.icons.extended"
}
