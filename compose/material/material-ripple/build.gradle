/*
 * Copyright 2020 The Android Open Source Project
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *      http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

import androidx.build.KmpPlatformsKt
import androidx.build.LibraryType

plugins {
    id("AndroidXPlugin")
    id("com.android.library")
    id("AndroidXComposePlugin")
}

def desktopEnabled = KmpPlatformsKt.enableDesktop(project)

androidXMultiplatform {
    android()
    if (desktopEnabled) desktop()

    sourceSets {
        commonMain {
            dependencies {
                implementation(libs.kotlinStdlibCommon)
                api(project(":compose:foundation:foundation"))
                api(project(":compose:runtime:runtime"))

                implementation(project(":compose:animation:animation"))
                implementation(project(":compose:ui:ui-util"))
            }
        }

        commonTest {
            dependencies {
            }
        }

        jvmMain {
            dependsOn(commonMain)
            dependencies {
            }
        }

        if (desktopEnabled) {
            skikoMain {
                dependsOn(commonMain)
                dependencies {
                    api(project(":compose:foundation:foundation"))
                    api(project(":compose:runtime:runtime"))
                    implementation(project(":compose:animation:animation"))
                    implementation(project(":compose:ui:ui-util"))
                }
            }
        }

        androidMain {
            dependsOn(jvmMain)
            dependencies {
            }
        }

        if (desktopEnabled) {
            desktopMain {
                dependsOn(skikoMain)
                dependsOn(jvmMain)
                dependencies {
                }
            }
        }

        jvmTest {
            dependsOn(commonTest)
            dependencies {
            }
        }

        androidAndroidTest {
            dependsOn(jvmTest)
            dependencies {
                implementation(project(":compose:test-utils"))

                implementation(libs.testRules)
                implementation(libs.testRunner)
                implementation(libs.junit)
                implementation(libs.truth)
            }
        }

        // TODO(b/214407011): These dependencies leak into instrumented tests as well. If you
        //  need to add Robolectric (which must be kept out of androidAndroidTest), use a top
        //  level dependencies block instead:
        //  `dependencies { testImplementation(libs.robolectric) }`
        androidTest {
            dependsOn(jvmTest)
            dependencies {
                implementation(libs.testRules)
                implementation(libs.testRunner)
                implementation(libs.junit)
                implementation(libs.truth)
            }
        }

        if (desktopEnabled) {
            desktopTest {
                dependsOn(jvmTest)
                dependsOn(desktopMain)
                dependencies {
                }
            }
        }
    }
}

androidx {
    name = "Compose Material Ripple"
    type = LibraryType.PUBLISHED_LIBRARY
    inceptionYear = "2020"
    description = "Material ripple used to build interactive components"
    // Disable strict API mode for MPP builds as it will fail to compile androidAndroidTest
    // sources, as it doesn't understand that they are tests and thinks they should have explicit
    // visibility
    legacyDisableKotlinStrictApiMode = true
}

android {
    namespace "androidx.compose.material.ripple"
}
