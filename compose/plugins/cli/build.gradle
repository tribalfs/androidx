/*
 * Copyright 2019 The Android Open Source Project
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *      http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */


import org.jetbrains.kotlin.gradle.tasks.KotlinCompile

import static androidx.build.dependencies.DependenciesKt.*
import androidx.build.CompilationTarget
import androidx.build.LibraryGroups
import androidx.build.LibraryVersions

buildscript {
    dependencies {
        classpath 'org.anarres.jarjar:jarjar-gradle:1.0.1'
    }
}

plugins {
    id("AndroidXPlugin")
    id("kotlin")
}

apply plugin: 'org.anarres.jarjar'

dependencies {
    api(KOTLIN_STDLIB)
    api("org.jetbrains.kotlin:kotlin-compiler")
    api("org.jetbrains.kotlin:kotlin-plugin")
    api("org.jetbrains.kotlin:kotlin-intellij-core")
    api("org.jetbrains.kotlin:kotlin-platform-api")
}

tasks.withType(KotlinCompile).configureEach {
    kotlinOptions {
        jvmTarget = "1.8"
    }
}

tasks.withType(KotlinCompile).all {
    kotlinOptions {
        freeCompilerArgs += ["-Werror"]
    }
}

jarjar.repackage('ideVersion') {
    destinationName "compose-cli-plugin-ide.jar"
    from files(sourceSets.main.output.classesDirs)
    from files(sourceSets.main.output.resourcesDir)
    dependsOn sourceSets.main.output
    classRename 'org.jetbrains.kotlin.com.**', 'com.@1'
}


configurations {
    pluginForIde
}

artifacts {
    pluginForIde(ideVersion.destinationPath) {
        name 'compose-cli-plugin-ide'
        type 'jar'
        builtBy ideVersion
    }
}

androidx {
    name = "AndroidX Compose Compiler Plugin"
    publish = true
    toolingProject = true
    mavenVersion = LibraryVersions.COMPOSE
    mavenGroup = LibraryGroups.COMPOSE
    inceptionYear = "2019"
    description = "Contains the Kotlin compiler plugin for Compose"
    compilationTarget = CompilationTarget.HOST
}
