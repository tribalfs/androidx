/*
 * Copyright 2019 The Android Open Source Project
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *      http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */


import androidx.build.AndroidXUiPlugin
import org.jetbrains.kotlin.gradle.tasks.KotlinCompile

import static androidx.build.dependencies.DependenciesKt.*
import androidx.build.LibraryGroups
import androidx.build.LibraryVersions
import androidx.build.Publish

plugins {
    id("AndroidXPlugin")
    id("AndroidXUiPlugin")
    id("com.android.library")
}

AndroidXUiPlugin.applyAndConfigureKotlinPlugin(project)

dependencies {
    kotlinPlugin project(":compose:compiler:compiler")

    if(!AndroidXUiPlugin.isMultiplatformEnabled(project)) {
        /*
         * When updating dependencies, make sure to make the an an analogous update in the
         * corresponding block below
         */

        api project(':compose:runtime:runtime-dispatch')
        api(KOTLIN_COROUTINES_ANDROID)

        implementation "androidx.annotation:annotation:1.1.0"
        implementation "org.jetbrains.kotlinx:kotlinx-collections-immutable-jvm:0.3"
        implementation("androidx.core:core-ktx:1.1.0")
        implementation(KOTLIN_REFLECT)
        implementation(KOTLIN_STDLIB)

        testImplementation KOTLIN_TEST_JUNIT
        testImplementation(JUNIT)
        testImplementation(ROBOLECTRIC)

        androidTestImplementation KOTLIN_TEST_JUNIT
        androidTestImplementation project(':compose:ui:ui')
        androidTestImplementation project(':ui:ui-test')
        androidTestImplementation(ANDROIDX_TEST_EXT_JUNIT)
        androidTestImplementation(ANDROIDX_TEST_RULES)
        androidTestImplementation(ANDROIDX_TEST_RUNNER)
        androidTestImplementation(JUNIT)
        androidTestImplementation(TRUTH)
    }
}

if(AndroidXUiPlugin.isMultiplatformEnabled(project)) {
    kotlin {
        android()
        jvm("desktop")

        /*
         * When updating dependencies, make sure to make the an an analogous update in the
         * corresponding block above
         */
        sourceSets {
            commonMain.dependencies {
                implementation(KOTLIN_STDLIB_COMMON)
                implementation(KOTLIN_COROUTINES_CORE)
                api project(':compose:runtime:runtime-dispatch')
                implementation "org.jetbrains.kotlinx:kotlinx-collections-immutable:0.3"
            }
            jvmMain.dependencies {
                implementation(KOTLIN_STDLIB)
                api(KOTLIN_COROUTINES_CORE)
            }
            androidMain {
                dependencies {
                    api(KOTLIN_COROUTINES_ANDROID)
                    api "androidx.annotation:annotation:1.1.0"

                    implementation("androidx.core:core-ktx:1.1.0")
                }
                dependsOn jvmMain
            }
            desktopMain {
                dependencies {
                    api(KOTLIN_COROUTINES_SWING)
                }
                dependsOn jvmMain
            }

            commonTest.dependencies {
                implementation kotlin("test-junit")
                implementation project(":compose:ui:ui")
            }
            androidAndroidTest.dependencies {
                implementation(ANDROIDX_TEST_EXT_JUNIT)
                implementation(ANDROIDX_TEST_RULES)
                implementation(ANDROIDX_TEST_RUNNER)
                implementation(TRUTH)
                implementation project(':compose:ui:ui')
                implementation project(':ui:ui-test')
            }
        }
    }
}

android {
    buildTypes {
        debug {
            testCoverageEnabled = false
        }
        release {
            testCoverageEnabled = false
        }
    }
}

androidx {
    name = "Compose Runtime"
    publish = Publish.SNAPSHOT_AND_RELEASE
    mavenVersion = LibraryVersions.COMPOSE
    mavenGroup = LibraryGroups.Compose.RUNTIME
    inceptionYear = "2019"
    description = "Tree composition support for code generated by the Compose compiler plugin and corresponding public API"
    legacyDisableKotlinStrictApiMode = true
}

tasks.withType(KotlinCompile).configureEach {
    kotlinOptions {
        useIR = true
        incremental = false
    }
}
