/*
 * Copyright 2020 The Android Open Source Project
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *      http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

import androidx.build.KmpPlatformsKt
import androidx.build.LibraryType

plugins {
    id("AndroidXPlugin")
    id("com.android.library")
    id("AndroidXComposePlugin")
}

def desktopEnabled = KmpPlatformsKt.enableDesktop(project)

androidXMultiplatform {
    android()
    if (desktopEnabled) desktop()

    sourceSets {
        commonMain {
            dependencies {
                implementation(libs.kotlinStdlibCommon)

                api(project(":compose:ui:ui-unit"))
                implementation(project(":compose:runtime:runtime"))
                implementation(project(":compose:ui:ui-util"))
            }
        }

        commonTest {
            dependencies {
                implementation(kotlin("test"))
            }
        }

        if (desktopEnabled) {
            skikoMain {
                dependsOn(commonMain)
                dependencies {
                    api(libs.skikoCommon)
                    implementation(project(":compose:runtime:runtime"))
                }
            }
        }

        jvmMain {
            dependsOn(commonMain)
            dependencies {
            }
        }


        androidMain {
            dependsOn(jvmMain)
            dependencies {
                api("androidx.annotation:annotation:1.2.0")
            }
        }

        if (desktopEnabled) {
            desktopMain {
                dependsOn(jvmMain)
                dependsOn(skikoMain)
                dependencies {
                    implementation(libs.kotlinStdlib)
                    implementation(libs.kotlinStdlibJdk8)
                }
            }
        }

        jvmTest {
            dependencies {
            }
        }

        androidAndroidTest {
            dependsOn(jvmTest)
            dependencies {
                implementation(project(":compose:ui:ui-graphics:ui-graphics-samples"))
                implementation(project(":compose:ui:ui-test-junit4"))
                implementation(project(":compose:test-utils"))
                implementation(libs.testRules)
                implementation(libs.testRunner)
                implementation(libs.espressoCore)
                implementation(libs.junit)
            }
        }

        // TODO(b/214407011): These dependencies leak into instrumented tests as well. If you
        //  need to add Robolectric (which must be kept out of androidAndroidTest), use a top
        //  level dependencies block instead:
        //  `dependencies { testImplementation(libs.robolectric) }`
        androidTest {
            dependsOn(jvmTest)
            dependencies {
                implementation(libs.testRules)
                implementation(libs.testRunner)
                implementation(libs.junit)
                implementation(libs.truth)
            }
        }

        if (desktopEnabled) {
            desktopTest {
                resources.srcDirs += "src/desktopTest/res"
                dependsOn(jvmTest)
                dependencies {
                    implementation(project(":compose:ui:ui-test-junit4"))
                    implementation(libs.junit)
                    implementation(libs.truth)
                    implementation(libs.skikoCurrentOs)
                }
            }
        }
    }
}

dependencies {
    lintPublish(project(":compose:ui:ui-graphics-lint"))
}

androidx {
    name = "Compose Graphics"
    type = LibraryType.PUBLISHED_LIBRARY
    inceptionYear = "2020"
    description = "Compose graphics"
    legacyDisableKotlinStrictApiMode = true
}

androidxCompose {
    composeCompilerPluginEnabled = false
}

android {
    namespace "androidx.compose.ui.graphics"
}

if (desktopEnabled) {
    tasks.findByName("desktopTest").configure {
        systemProperties["GOLDEN_PATH"] = project.rootDir.absolutePath + "/../../golden"
    }
}
