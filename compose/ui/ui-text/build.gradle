/*
 * Copyright 2019 The Android Open Source Project
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *      http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

import androidx.build.KmpPlatformsKt
import androidx.build.LibraryType

plugins {
    id("AndroidXPlugin")
    id("com.android.library")
    id("AndroidXComposePlugin")
}

def desktopEnabled = KmpPlatformsKt.enableDesktop(project)

androidXMultiplatform {
    android()
    if (desktopEnabled) desktop()

    sourceSets {
        commonMain {
            dependencies {
                implementation(libs.kotlinStdlibCommon)
                implementation(libs.kotlinCoroutinesCore)

                api(project(":compose:ui:ui-graphics"))
                api(project(":compose:ui:ui-unit"))

                // when updating the runtime version please also update the runtime-saveable version
                implementation(project(":compose:runtime:runtime"))
                implementation(project(":compose:runtime:runtime-saveable"))

                implementation(project(":compose:ui:ui-util"))
            }
        }

        commonTest {
            dependencies {
            }
        }

        if (desktopEnabled) {
            skikoMain {
                dependsOn(commonMain)
                dependencies {
                    api(libs.skikoCommon)
                    implementation(project(":compose:runtime:runtime"))
                    implementation(project(":compose:runtime:runtime-saveable"))
                }
            }
        }

        jvmMain {
            dependsOn(commonMain)
            dependencies {
                implementation(libs.kotlinStdlib)
            }
        }


        androidMain {
            dependsOn(commonMain)
            dependsOn(jvmMain)
            dependencies {
                api("androidx.annotation:annotation:1.1.0")
                implementation("androidx.core:core:1.7.0")
                implementation("androidx.emoji2:emoji2:1.2.0")
                implementation('androidx.collection:collection:1.0.0')
            }
        }

        if (desktopEnabled) {
            desktopMain {
                dependsOn(skikoMain)
                dependsOn(jvmMain)
            }
        }

        jvmTest {
            dependencies {
            }
        }

        androidAndroidTest {
            dependsOn(jvmTest)
            dependencies {
                implementation(project(":compose:ui:ui-test-junit4"))
                implementation(project(":internal-testutils-fonts"))
                implementation(libs.testRules)
                implementation(libs.testRunner)
                implementation(libs.espressoCore)
                implementation(libs.junit)
                implementation(libs.dexmakerMockito)
                implementation(libs.mockitoCore)
                implementation(libs.truth)
                implementation(libs.mockitoKotlin)
            }
        }


        // TODO(b/214407011): These dependencies leak into instrumented tests as well. If you
        //  need to add Robolectric (which must be kept out of androidAndroidTest), use a top
        //  level dependencies block instead:
        //  `dependencies { testImplementation(libs.robolectric) }`
        androidTest {
            dependsOn(jvmTest)
            dependencies {
                implementation(project(":internal-testutils-fonts"))
                implementation(libs.testRules)
                implementation(libs.testRunner)
                implementation(libs.junit)
                implementation(libs.truth)
                implementation(libs.kotlinReflect)
                implementation(libs.kotlinTest)
            }
        }

        if (desktopEnabled) {
            desktopTest {
                dependsOn(jvmTest)
                dependencies {
                    implementation(libs.truth)
                    implementation(libs.junit)
                    implementation(libs.kotlinTest)
                    implementation(libs.skikoCurrentOs)
                    implementation(project(":compose:foundation:foundation"))
                    implementation(project(":compose:ui:ui-test-junit4"))
                    implementation(project(":internal-testutils-fonts"))
                }
            }
        }

        androidMain.kotlin.srcDirs("${supportRootFolder}/text/text/src/main/java")
    }
}

dependencies {
    // Can't declare this in kotlin { sourceSets { androidTest.dependencies { .. } } } as that
    // leaks into instrumented tests (b/214407011)
    testImplementation(libs.mockitoCore4)
    testImplementation(libs.mockitoKotlin4)
}

androidx {
    name = "Compose UI Text"
    type = LibraryType.PUBLISHED_LIBRARY
    inceptionYear = "2019"
    description = "Compose Text primitives and utilities"
    legacyDisableKotlinStrictApiMode = true
}

android {
    namespace "androidx.compose.ui.text"
}
