/*
 * Copyright 2019 The Android Open Source Project
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *      http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */


import androidx.build.KmpPlatformsKt
import androidx.build.LibraryType

plugins {
    id("AndroidXPlugin")
    id("com.android.library")
    id("AndroidXComposePlugin")
}

def desktopEnabled = KmpPlatformsKt.enableDesktop(project)

androidXMultiplatform {
    android()
    if (desktopEnabled) desktop()

    sourceSets {
        commonMain {
            dependencies {
                implementation(libs.kotlinStdlib)

                api(project(":compose:runtime:runtime"))
                api(project(":compose:ui:ui"))
            }
        }

        commonTest {
            dependencies {
                implementation(kotlin("test-junit"))
            }
        }

        jvmMain {
            dependsOn(commonMain)
            dependencies {
                implementation(libs.kotlinStdlib)
            }
        }

        if (desktopEnabled) {
            skikoMain {
                dependsOn(commonMain)
                dependencies {

                }
            }
        }

        androidMain {
            dependsOn(jvmMain)
            dependencies {
                api("androidx.annotation:annotation:1.1.0")
            }
        }

        if (desktopEnabled) {
            desktopMain {
                dependsOn(skikoMain)
                dependsOn(jvmMain)
                dependencies {

                }
            }
        }

        jvmTest {
            dependsOn(commonTest)
            dependencies {
            }
        }

        androidAndroidTest {
            dependsOn(jvmTest)
            dependencies {
                implementation(project(":compose:ui:ui-test-junit4"))

                implementation(libs.junit)
                implementation(libs.testCore)
                implementation(libs.testRunner)
                implementation(libs.testRules)

                implementation(libs.truth)
                implementation(project(":compose:foundation:foundation-layout"))
                implementation(project(":compose:foundation:foundation"))
                implementation(project(":compose:material:material"))
                implementation("androidx.activity:activity-compose:1.3.1")
            }
        }

        androidTest {
            dependsOn(jvmTest)
            dependencies {
                implementation(libs.truth)
            }
        }

        if (desktopEnabled) {
            desktopTest {
                dependsOn(jvmTest)
                dependsOn(desktopMain)
                dependencies {

                }
            }
        }
    }
}

androidx {
    name = "Compose Tooling Data"
    type = LibraryType.PUBLISHED_LIBRARY
    inceptionYear = "2021"
    description = "Compose tooling library data. This library provides data about compose" +
            " for different tooling purposes."
    legacyDisableKotlinStrictApiMode = true
}

android {
    namespace "androidx.compose.ui.tooling.data"
}
