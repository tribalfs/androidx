/*
 * Copyright 2019 The Android Open Source Project
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *      http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */


import androidx.build.AndroidXUiPlugin
import androidx.build.LibraryGroups
import androidx.build.LibraryType

import static androidx.build.dependencies.DependenciesKt.*

plugins {
    id("AndroidXPlugin")
    id("com.android.library")
    id("AndroidXUiPlugin")
}

AndroidXUiPlugin.applyAndConfigureKotlinPlugin(project)

dependencies {
    kotlinPlugin(project(":compose:compiler:compiler"))

    if(!AndroidXUiPlugin.isMultiplatformEnabled(project)) {
        implementation(KOTLIN_STDLIB)

        api("androidx.annotation:annotation:1.1.0")

        api(project(":compose:runtime:runtime"))
        api(project(":compose:ui:ui"))
        api(project(":compose:ui:ui-tooling-data"))
        implementation(project(":compose:material:material"))
        implementation "androidx.activity:activity-ktx:1.2.0"

        // kotlin-reflect and tooling-animation-internal are provided by Studio at runtime
        compileOnly(project(":ui:ui-animation-tooling-internal"))
        compileOnly(KOTLIN_REFLECT)

        androidTestImplementation(project(":compose:ui:ui-test-junit4"))

        androidTestImplementation(JUNIT)
        androidTestImplementation(ANDROIDX_TEST_RUNNER)
        androidTestImplementation(ANDROIDX_TEST_RULES)
        androidTestImplementation(project(":compose:foundation:foundation-layout"))
        androidTestImplementation(project(":compose:foundation:foundation"))
        androidTestImplementation(project(":compose:test-utils"))
        androidTestImplementation(TRUTH)
        androidTestImplementation(KOTLIN_REFLECT)
        androidTestImplementation(project(":ui:ui-animation-tooling-internal"))
    }
}

if(AndroidXUiPlugin.isMultiplatformEnabled(project)) {
    kotlin {
        android()
        jvm("desktop")

        /*
         * When updating dependencies, make sure to make the an an analogous update in the
         * corresponding block above
         */
        sourceSets {
            commonMain.dependencies {
                implementation(KOTLIN_STDLIB_COMMON)

            }
            androidMain.dependencies {
                api("androidx.annotation:annotation:1.1.0")

                api(project(":compose:runtime:runtime"))
                api(project(":compose:ui:ui"))
                api(project(":compose:ui:ui-tooling-data"))
                implementation(project(":compose:material:material"))
                implementation "androidx.activity:activity-ktx:1.2.0"

                // kotlin-reflect and tooling-animation-internal are provided by Studio at runtime
                compileOnly(project(":ui:ui-animation-tooling-internal"))
                compileOnly(KOTLIN_REFLECT)
            }

            androidMain.dependsOn(jvmMain)
            desktopMain.dependsOn(jvmMain)

            desktopMain.dependencies {
                implementation(KOTLIN_STDLIB)
                implementation(project(":compose:runtime:runtime"))
                implementation(project(":compose:ui:ui"))
            }

            androidAndroidTest.dependencies {
                implementation(project(":compose:ui:ui-test-junit4"))

                implementation(JUNIT)
                implementation(ANDROIDX_TEST_RUNNER)
                implementation(ANDROIDX_TEST_RULES)
                implementation(project(":compose:foundation:foundation-layout"))
                implementation(project(":compose:foundation:foundation"))
                implementation(project(":compose:test-utils"))
                implementation(TRUTH)
                implementation(KOTLIN_REFLECT)
                implementation(project(":ui:ui-animation-tooling-internal"))
            }
        }
    }
}


androidx {
    name = "Compose Tooling"
    type = LibraryType.PUBLISHED_LIBRARY
    mavenGroup = LibraryGroups.Compose.UI
    inceptionYear = "2019"
    description = "Compose tooling library. This library exposes information to our tools for better IDE support."
    legacyDisableKotlinStrictApiMode = true
}
