import static androidx.build.dependencies.DependenciesKt.*
import androidx.build.LibraryGroups
import androidx.build.LibraryVersions
import androidx.build.Publish

plugins {
    id("AndroidXPlugin")
    id("com.android.library")
    id("kotlin-android")
}

dependencies {
    api("androidx.annotation:annotation:1.2.0-rc01")
    api("androidx.lifecycle:lifecycle-runtime:2.0.0")
    api("androidx.versionedparcelable:versionedparcelable:1.1.1")
    implementation("androidx.collection:collection:1.0.0")

    // We don't ship this as a public artifact, so it must remain a project-type dependency.
    annotationProcessor(project(":versionedparcelable:versionedparcelable-compiler"))

    androidTestImplementation(KOTLIN_STDLIB)
    androidTestImplementation(ANDROIDX_TEST_EXT_JUNIT)
    androidTestImplementation(ANDROIDX_TEST_CORE)
    androidTestImplementation(ANDROIDX_TEST_RUNNER)
    androidTestImplementation(ANDROIDX_TEST_RULES)
    androidTestImplementation(TRUTH)
    androidTestImplementation(ESPRESSO_CORE, libs.exclude_for_espresso)
    androidTestImplementation(MOCKITO_CORE, libs.exclude_bytebuddy) // DexMaker has it"s own MockMaker
    androidTestImplementation(MULTIDEX)

    // Including both dexmakers allows support for all API levels plus final mocking support on
    // API 28+. The implementation is swapped based on the finality of the mock type. This
    // delegation is handled manually inside androidx.core.util.mockito.CustomMockMaker.
    androidTestImplementation(DEXMAKER_MOCKITO, libs.exclude_bytebuddy) // DexMaker has it"s own MockMaker
    androidTestImplementation(DEXMAKER_MOCKITO_INLINE, libs.exclude_bytebuddy) // DexMaker has it"s own MockMaker
    androidTestImplementation("androidx.appcompat:appcompat:1.1.0") {
        exclude group: "androidx.core", module: "core"
    }
    androidTestImplementation(project(":internal-testutils-runtime"), {
        exclude group: "androidx.core", module: "core"
    })
    androidTestImplementation(project(":internal-testutils-mockito"))

    testImplementation(ANDROIDX_TEST_CORE)
    testImplementation(ANDROIDX_TEST_RUNNER)
    testImplementation(TRUTH)
    testImplementation(ROBOLECTRIC)
}

android {
    buildFeatures {
        aidl = true
    }
    testOptions.unitTests.includeAndroidResources = true
    compileOptions {
        sourceCompatibility = JavaVersion.VERSION_1_7
        targetCompatibility = JavaVersion.VERSION_1_7
    }
    aaptOptions {
        noCompress "ttf"
    }
    buildTypes.all {
        consumerProguardFiles "proguard-rules.pro"
    }

    packagingOptions {
        // Drop the file from external dependencies, preferring the local file inside androidTest
        pickFirsts = [
                "mockito-extensions/org.mockito.plugins.MockMaker",
                "mockito-extensions/org.mockito.plugins.StackTraceCleanerProvider"
        ]
    }
    defaultConfig {
        multiDexEnabled = true
    }
}

androidx {
    name = "Android Support Library compat"
    publish = Publish.SNAPSHOT_AND_RELEASE
    mavenVersion = LibraryVersions.CORE
    mavenGroup = LibraryGroups.CORE
    inceptionYear = "2015"
    description = "The Support Library is a static library that you can add to your Android application in order to use APIs that are either not available for older platform versions or utility APIs that aren\'t a part of the framework APIs. Compatible on devices running API 14 or later."
    failOnDeprecationWarnings = false
}
