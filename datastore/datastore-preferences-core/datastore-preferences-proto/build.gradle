/*
 * Copyright 2020 The Android Open Source Project
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *      http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */


import org.anarres.gradle.plugin.jarjar.JarjarTask

import static androidx.build.dependencies.DependenciesKt.*
import androidx.build.LibraryGroups
import androidx.build.Publish


buildscript {
    dependencies {
        classpath 'org.anarres.jarjar:jarjar-gradle:1.0.1'
    }
}


plugins {
    id("AndroidXPlugin")
    id("kotlin")
    id("com.google.protobuf")
}

apply plugin: 'org.anarres.jarjar'


dependencies {
    implementation(PROTOBUF_LITE)
    api(project(":datastore:datastore-core"))
}

protobuf {
    protoc {
        artifact = PROTOBUF_COMPILER
    }

    // Generates the java proto-lite code for the protos in this project. See
    // https://github.com/google/protobuf-gradle-plugin#customizing-protobuf-compilation
    // for more information.
    generateProtoTasks {
        all().each { task ->
            task.builtins {
                java {
                    option 'lite'
                }
            }
        }
    }
}

// The proto-lite dependency includes .proto files, which are not used by datastore. When apps
// depend on datastore as well as proto-lite directly, these files conflict since jarjar only
// renames the java classes. Remove them here since they are unused.
tasks.register("protoLiteWithoutProtoFiles", Copy) {
    from(zipTree(configurations.detachedConfiguration(dependencies.create
            (PROTOBUF_LITE)).getSingleFile())) {
                exclude("**/*.proto")
    }

    destinationDir new File(project.buildDir, "protoLiteWithoutProtoFiles")
}

def preferencesProtoJarJarTask = tasks.register("jarjar", JarjarTask){
    dependsOn(protobuf.generateProtoTasks.all())
    from(files(protobuf.generatedFilesBaseDir))
    dependsOn(compileKotlin)
    from(files(compileKotlin.outputs.files))

    dependsOn(compileJava)
    from(files(compileJava.destinationDirectory))

    dependsOn(protoLiteWithoutProtoFiles)
    from(files(protoLiteWithoutProtoFiles.destinationDir))
    classRename 'com.google.protobuf.**', 'androidx.datastore.preferences.protobuf.@1'
}

def jarjarConf = configurations.register("jarjarpreferences")

artifacts.add("${jarjarConf.name}", preferencesProtoJarJarTask.get().destinationPath) {
    name "datastore-preferences-proto-jarjar"
    type 'jar'
    builtBy preferencesProtoJarJarTask
}

androidx {
    name = "Android Preferences DataStore Proto"
    publish = Publish.NONE
    mavenGroup = LibraryGroups.DATASTORE
    inceptionYear = "2020"
    description = "Jarjar the generated proto and proto-lite dependency for use by " +
            "datastore-preferences."
}
