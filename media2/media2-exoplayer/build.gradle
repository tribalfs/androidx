/*
 * Copyright 2018 The Android Open Source Project
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *      http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

import androidx.build.LibraryGroups
import androidx.build.LibraryVersions

import static androidx.build.dependencies.DependenciesKt.CHECKER_FRAMEWORK

plugins {
    id("SupportAndroidLibraryPlugin")
}

android {
    buildTypes.all {
        consumerProguardFiles 'proguard-rules.pro'
    }
}

dependencies {
    compileOnly(CHECKER_FRAMEWORK)
}

// Copy in classes from the prebuilt at the end of the java compilation task.
afterEvaluate {
    android.libraryVariants.all { variant ->
        project.tasks.getByName("compile${variant.name.capitalize()}JavaWithJavac").doLast {
            copy {
              from zipTree("${projectDir}/src/main/libs/exoplayer-media2.jar")
              into("${buildDir}/intermediates/javac/${variant.name}/" +
                      "compile${variant.name.capitalize()}JavaWithJavac/classes")
            }
        }
    }
}

supportLibrary {
    name = "Media2 repackaged ExoPlayer dependency"
    publish = true
    mavenVersion = LibraryVersions.MEDIA2_EXOPLAYER
    mavenGroup = LibraryGroups.MEDIA2
    inceptionYear = "2018"
    description = "Repackaged ExoPlayer for 'media2' artifact"
}
