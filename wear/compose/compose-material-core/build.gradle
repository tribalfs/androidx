/*
 * Copyright 2022 The Android Open Source Project
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *      http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

import androidx.build.LibraryType
import androidx.build.AndroidXComposePlugin
import org.jetbrains.kotlin.gradle.tasks.KotlinCompile

plugins {
    id("AndroidXPlugin")
    id("com.android.library")
    id("AndroidXComposePlugin")
}

// Disable multi-platform.
AndroidXComposePlugin.applyAndConfigureKotlinPlugin(project, /* isMultiplatformEnabled= */ false)

dependencies {
    api("androidx.compose.foundation:foundation:1.4.0-beta02")
    api("androidx.compose.ui:ui:1.4.0-beta02")
    api("androidx.compose.ui:ui-text:1.4.0-beta02")
    api("androidx.compose.runtime:runtime:1.4.0-beta02")

    implementation(libs.kotlinStdlib)
    implementation("androidx.compose.animation:animation:1.4.0-beta02")
    implementation("androidx.compose.material:material-icons-core:1.4.0-beta02")
    implementation("androidx.compose.material:material-ripple:1.4.0-beta02")
    implementation("androidx.compose.ui:ui-util:1.4.0-beta02")
    implementation(project(":wear:compose:compose-foundation"))
    implementation("androidx.profileinstaller:profileinstaller:1.2.0")

    androidTestImplementation("androidx.compose.ui:ui-test:1.4.0-beta02")
    androidTestImplementation("androidx.compose.ui:ui-test-junit4:1.4.0-beta02")
    androidTestImplementation(project(":compose:test-utils"))

    androidTestImplementation(project(":test:screenshot:screenshot"))
    androidTestImplementation(libs.testRunner)
    androidTestImplementation(libs.truth)

    testImplementation(libs.testRules)
    testImplementation(libs.testRunner)
    testImplementation(libs.junit)
}

android {
    defaultConfig {
        minSdkVersion 25
    }
    // Use Robolectric 4.+
    testOptions.unitTests.includeAndroidResources = true
    sourceSets.androidTest.assets.srcDirs +=
            project.rootDir.absolutePath + "/../../golden/wear/compose/materialcore"
    buildTypes.all {
        consumerProguardFiles("proguard-rules.pro")
    }
    namespace "androidx.wear.compose.material.core"
    lint {
        baseline = file("lint-baseline.xml")
    }
}

androidx {
    name = "Android Wear Compose Material Core"
    type = LibraryType.PUBLISHED_LIBRARY
    inceptionYear = "2022"
    description = "WearOS Compose Material Core Library. This library contains themeless " +
            "components that are shared between different WearOS Compose Material libraries. It " +
            "builds upon the Jetpack Compose libraries."
    targetsJavaConsumers = false
}

tasks.withType(KotlinCompile).configureEach {
    kotlinOptions {
        freeCompilerArgs += [
                "-Xjvm-default=all",
        ]
    }
}
