/*
 * Copyright (C) 2017 The Android Open Source Project
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *      http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */


import androidx.build.LibraryGroups
import androidx.build.LibraryVersions
import androidx.build.AndroidXExtension

import static androidx.build.dependencies.DependenciesKt.*
import androidx.build.Publish

plugins {
    id("AndroidXPlugin")
    id("com.android.library")
    id("kotlin-android")
}

android {
    compileOptions {
        sourceCompatibility = JavaVersion.VERSION_1_7
        targetCompatibility = JavaVersion.VERSION_1_7
    }
    buildTypes {
        debug {
            // Breaks Kotlin compiler
            testCoverageEnabled = false
        }
    }
}

dependencies {
    api project(':work:work-runtime')
    implementation(GCM_NETWORK_MANAGER)
    // Using -PuseMaxDepVersions does not use the right version of the annotation processor
    // Remove this workaround after b/127495641 is fixed
    if (project.hasProperty('useMaxDepVersions')) {
        annotationProcessor(project(":room:room-compiler"))
        implementation(project(":room:room-runtime"))
        androidTestImplementation(project(":room:room-testing"))
    } else {
        annotationProcessor(ARCH_ROOM_COMPILER)
        implementation(ARCH_ROOM_RUNTIME)
        androidTestImplementation(ARCH_ROOM_TESTING)
    }

    androidTestImplementation(project(":work:work-runtime-ktx"))
    androidTestImplementation(ANDROIDX_TEST_EXT_JUNIT)
    androidTestImplementation(ANDROIDX_TEST_CORE)
    androidTestImplementation(ANDROIDX_TEST_RUNNER)
    androidTestImplementation(ESPRESSO_CORE)
    androidTestImplementation(MOCKITO_CORE, libs.exclude_bytebuddy) // DexMaker has its own MockMaker
    androidTestImplementation(DEXMAKER_MOCKITO, libs.exclude_bytebuddy) // DexMaker has its own MockMaker
    testImplementation(JUNIT)
}

androidx {
    name = "Android WorkManager GCMNetworkManager Support"
    publish = Publish.SNAPSHOT_AND_RELEASE
    mavenVersion = LibraryVersions.WORK
    mavenGroup = LibraryGroups.WORK
    inceptionYear = "2019"
    description = "Android WorkManager GCMNetworkManager Support"
    url = AndroidXExtension.ARCHITECTURE_URL
    failOnDeprecationWarnings = false
}
